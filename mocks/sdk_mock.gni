# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../cjc_toolchain.gni")

template("mock_sdk_target") {
  all_deps = []
  all_sources = []

  foreach(source, invoker.sources) {
    _sourcename_ = get_path_info(source, "name")
    _inputpath_ = rebase_path(source)
    _outcpath_ = "$target_gen_dir/${_sourcename_}.c"
    _outcpath_abs_ = rebase_path(_outcpath_)

    action("gen_src_${_sourcename_}") {
      sources = [ source ]
      script = "${cjc_script_root}/generate_mock.py"
      args = [
        "--input=${_inputpath_}",
        "--output=${_outcpath_abs_}",
      ]
      outputs = [_outcpath_]
    }

    all_deps += [ ":gen_src_${_sourcename_}" ]
    all_sources += [ _outcpath_ ]
  }

  ohos_shared_library(target_name) {
    forward_variables_from(invoker, [
      "subsystem_name",
      "part_name",
      "output_extension",
    ])
    sources = all_sources
    if (defined(invoker.c_sources)) {
      sources += invoker.c_sources
    }
    deps = all_deps
    deps += [
      "${cjc_script_root}/mocks:mock_stub"
    ]
  }
}